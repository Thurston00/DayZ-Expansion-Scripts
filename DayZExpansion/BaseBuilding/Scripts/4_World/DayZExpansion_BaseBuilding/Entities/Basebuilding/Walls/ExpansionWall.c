/**
 * ExpansionWallBase.c
 *
 * DayZ Expansion Mod
 * www.dayzexpansion.com
 * © 2022 DayZ Expansion Mod Team
 *
 * This work is licensed under the Creative Commons Attribution-NonCommercial-NoDerivatives 4.0 International License. 
 * To view a copy of this license, visit http://creativecommons.org/licenses/by-nc-nd/4.0/.
 *
*/

class ExpansionWallBase: ExpansionBaseBuilding
{
	protected bool m_Expansion_IsOpened1;
	protected bool m_Expansion_IsOpened2;
	protected bool m_Expansion_IsOpened3;
	protected bool m_Expansion_IsOpened4;

	protected bool m_Expansion_WasOpen;
	protected bool m_Expansion_WasOpen1;
	protected bool m_Expansion_WasOpen2;

	protected bool m_Expansion_HasWindow;
	protected bool m_Expansion_HasDoor;
	protected bool m_Expansion_HasGate;
	protected bool m_Expansion_HasWall;


	void ExpansionWallBase()
	{
		RegisterNetSyncVariableBool( "m_Expansion_IsOpened1" );
		RegisterNetSyncVariableBool( "m_Expansion_IsOpened2" );
		RegisterNetSyncVariableBool( "m_Expansion_IsOpened3" );
		RegisterNetSyncVariableBool( "m_Expansion_IsOpened4" );
		RegisterNetSyncVariableBool( "m_Expansion_HasWindow" );
		RegisterNetSyncVariableBool( "m_Expansion_HasDoor" );
		RegisterNetSyncVariableBool( "m_Expansion_HasGate" );
		RegisterNetSyncVariableBool( "m_Expansion_HasWall" );

		m_Expansion_CurrentBuild = "wood";
	}

	override bool IsLastStage()
	{
		return m_Expansion_HasDoor || m_Expansion_HasGate || m_Expansion_HasWindow || m_Expansion_HasWall;
	}

	override bool IsLastStageBuilt()
	{
		return IsPartBuilt( m_Expansion_CurrentBuild + "_wallfinished" ) || IsPartBuilt( m_Expansion_CurrentBuild + "_wallfinished_half" ) || IsPartBuilt( m_Expansion_CurrentBuild + "_wallfinished_third" );
	}

	override string GetConstructionKitType()
	{
		return "ExpansionWallKit";
	}

	override bool NameOverride(out string output)
	{
		if (IsLastStage())
			output = "#STR_EXPANSION_BB_" + m_Expansion_CurrentBuild + "_WALL_FINISHED";
		else
			output = "#STR_EXPANSION_BB_" + m_Expansion_CurrentBuild + "_WALL_BASE";
		return true;
	}

	override bool CanBeDamaged()
	{
		auto settings = GetExpansionSettings().GetRaid();
		if (settings.BaseBuildingRaidMode == ExpansionBaseBuildingRaidMode.DoorsGates)
		{
			return (m_Expansion_HasDoor || m_Expansion_HasGate) && settings.IsRaidableNow();
		}
		else if (settings.BaseBuildingRaidMode == ExpansionBaseBuildingRaidMode.DoorsGatesWindows)
		{
			return (m_Expansion_HasDoor || m_Expansion_HasGate || m_Expansion_HasWindow) && settings.IsRaidableNow();
		}
		else if (settings.BaseBuildingRaidMode == ExpansionBaseBuildingRaidMode.DoorsGatesWindowsWalls)
		{
			return settings.IsRaidableNow();
		}
		
		return super.CanBeDamaged();
	}

	#ifdef EXPANSION_MODSTORAGE
	override void CF_OnStoreSave(CF_ModStorageMap storage)
	{
		super.CF_OnStoreSave(storage);

		auto ctx = storage[DZ_Expansion_BaseBuilding];
		if (!ctx) return;

		ctx.Write(m_Expansion_HasWindow);
		ctx.Write(m_Expansion_HasDoor);
		ctx.Write(m_Expansion_HasGate);
		ctx.Write(m_Expansion_HasWall);
		ctx.Write(m_Expansion_IsOpened);
		ctx.Write(m_Expansion_IsOpened1);
		ctx.Write(m_Expansion_IsOpened2);
		ctx.Write(m_Expansion_IsOpened3);
		ctx.Write(m_Expansion_IsOpened4);
	}
	
	override bool CF_OnStoreLoad(CF_ModStorageMap storage)
	{
		if (!super.CF_OnStoreLoad(storage))
			return false;

		auto ctx = storage[DZ_Expansion_BaseBuilding];
		if (!ctx) return true;

		if (!ctx.Read(m_Expansion_HasWindow))
			return false;
			
		if (!ctx.Read(m_Expansion_HasDoor))
			return false;

		if (!ctx.Read(m_Expansion_HasGate))
			return false;

		if (!ctx.Read(m_Expansion_HasWall))
			return false;

		if (!ctx.Read(m_Expansion_IsOpened))
			return false;

		if (!ctx.Read(m_Expansion_IsOpened1))
			return false;
			
		if (!ctx.Read(m_Expansion_IsOpened2))
			return false;

		if (!ctx.Read(m_Expansion_IsOpened3))
			return false;

		if (!ctx.Read(m_Expansion_IsOpened4))
			return false;

		return true;
	}
	#endif

	override bool HasBase()
	{
		return (GetConstruction().IsPartConstructed( "wood_base" ) || GetConstruction().IsPartConstructed( "wood_base_third" ) || GetConstruction().IsPartConstructed( "wood_base_half" ));
	}

	bool HasDoor()
	{
		return m_Expansion_HasDoor;
	}

	bool HasGate()
	{
		return m_Expansion_HasGate;
	}

	bool HasWindow()
	{
		return m_Expansion_HasWindow;
	}

	override void OnVariablesSynchronized()
	{
		bool wasSynched = m_Expansion_WasSynced;

		super.OnVariablesSynchronized();

		bool door_changed;
		//! https://feedback.bistudio.com/T173348
		if (m_Expansion_HasDoor && m_Expansion_WasOpen != m_Expansion_IsOpened)
			door_changed = true;
		bool door_opened;
		if (door_changed && m_Expansion_IsOpened)
			door_opened = true;

		bool gate_l_changed;
		if (m_Expansion_HasGate && m_Expansion_WasOpen1 != m_Expansion_IsOpened1)
			gate_l_changed = true;
		bool gate_l_opened;
		if (gate_l_changed && m_Expansion_IsOpened1)
			gate_l_opened = true;

		bool gate_r_changed;
		if (m_Expansion_HasGate && m_Expansion_WasOpen2 != m_Expansion_IsOpened2)
			gate_r_changed = true;
		bool gate_r_opened;
		if (gate_r_changed && m_Expansion_IsOpened2)
			gate_r_opened = true;

		if ( wasSynched && ( door_changed || gate_l_changed || gate_r_changed ) )
		{
			if ( door_opened || gate_l_opened || gate_r_opened )
				SoundGateOpenStart();
			else
				SoundGateCloseStart();
		}

		m_Expansion_WasOpen = m_Expansion_IsOpened;
		m_Expansion_WasOpen1 = m_Expansion_IsOpened1;
		m_Expansion_WasOpen2 = m_Expansion_IsOpened2;

		UpdateVisuals();
	}
	
	override void AfterStoreLoad()
	{
#ifdef EXPANSIONTRACE
		auto trace = CF_Trace_0(ExpansionTracing.CE, this, "AfterStoreLoad");
#endif
	
		super.AfterStoreLoad();

		if ( m_ExpansionSaveVersion < 18 )
			m_Expansion_HasWall = IsLastStageBuilt();
		
		UpdateVisuals();

		//! Restore state of opened windows/door/gate
		if ( m_Expansion_IsOpened || m_Expansion_IsOpened1 || m_Expansion_IsOpened2 || m_Expansion_IsOpened3 || m_Expansion_IsOpened4 )
			Expansion_Open( "" );
	}

	override bool CanReleaseAttachment( EntityAI attachment )
	{
		if ( attachment.IsInherited( ExpansionCodeLock ) )
		{
			ExpansionCodeLock codelock = ExpansionCodeLock.Cast( attachment );
			if ( !codelock.ExpansionIsLocked() )
			{
				return true;
			}
		}

		return super.CanReleaseAttachment( attachment );
	}

	override bool ExpansionHasCodeLock( string selection )
	{
		auto settings = GetExpansionSettings().GetBaseBuilding(false);

		if (!settings.IsLoaded())
			return false;

		if ( selection == "codelock_door" || settings.CodelockActionsAnywhere )
		{
			if ( m_Expansion_HasDoor && FindAttachmentBySlotName( "Att_ExpansionCodeLock_1" ) )
			{
				return true;
			}
		}

		if ( selection == "codelock_gate" || settings.CodelockActionsAnywhere )
		{
			if ( m_Expansion_HasGate && FindAttachmentBySlotName( "Att_ExpansionCodeLock_2" ) )
			{
				return true;
			}
		}

		return false;
	}

	override ExpansionCodeLock ExpansionGetCodeLock()
	{
		if (m_Expansion_HasDoor)
			return ExpansionCodeLock.Cast(FindAttachmentBySlotName("Att_ExpansionCodeLock_1"));

		if (m_Expansion_HasGate)
			return ExpansionCodeLock.Cast(FindAttachmentBySlotName("Att_ExpansionCodeLock_2"));

		return null;
	}

	override bool ExpansionGetCollisionBox( out vector minMax[2] )
	{
		minMax[0] = "-3 0 -0.125";
		minMax[1] = "3 3 0.125";
		return true;
	}

	override bool CanPutInCargo( EntityAI parent )
	{
		return false;
	}

	override bool IsPlayerInside( PlayerBase player, string selection )
	{
		if ( !IsLastStage() )
			return true;

		vector player_pos = player.GetPosition();
		vector wall_pos = GetPosition();

		//! Don't allow dismantling from below/above
		if (player_pos[1] >= wall_pos[1] + 3 || player_pos[1] + 3 <= wall_pos[1])
			return false;

		vector minMax[2];
		ExpansionGetCollisionBox(minMax);
		minMax[0][2] = -3;
		minMax[1][2] = 0;

		vector test_position = WorldToModel(player_pos);

		return Math.IsPointInRectangle(minMax[0], minMax[1], test_position);
	}

	override bool ExpansionIsOpenable()
	{
		return m_Expansion_HasWindow || m_Expansion_HasDoor || m_Expansion_HasGate;
	}

	override bool ExpansionIsOpenable( string selection )
	{
		if ( m_Expansion_HasWindow )
		{
			if ( selection == m_Expansion_CurrentBuild + "_window_ll" )
				return true;
			if ( selection == m_Expansion_CurrentBuild + "_window_lr" )
				return true;
			if ( selection == m_Expansion_CurrentBuild + "_window_rl" )
				return true;
			if ( selection == m_Expansion_CurrentBuild + "_window_rr" )
				return true;
		}
		else if ( m_Expansion_HasDoor )
		{
			if ( selection == m_Expansion_CurrentBuild + "_door" )
				return true;
		} else if ( m_Expansion_HasGate )
		{
			if ( selection == m_Expansion_CurrentBuild + "_gate_l" )
				return true;
			if ( selection == m_Expansion_CurrentBuild + "_gate_r" )
				return true;
		}

		return false;
	}

	bool ExpansionHasGate()
	{
		return m_Expansion_HasGate;
	}

	bool ExpansionHasDoor()
	{
		return m_Expansion_HasDoor;
	}
	
	void ExpansionResetBaseBuildingState()
	{
		m_Expansion_HasWindow = false;
		m_Expansion_HasDoor = false;
		m_Expansion_HasGate = false;
		m_Expansion_HasWall = false;

		m_Expansion_IsOpened = false;
		m_Expansion_IsOpened1 = false;
		m_Expansion_IsOpened2 = false;
		m_Expansion_IsOpened3 = false;
		m_Expansion_IsOpened4 = false;
	}

	override void OnPartBuiltServer( notnull Man player, string part_name, int action_id )
	{
		ExpansionResetBaseBuildingState();

		ExpansionUpdateBaseBuildingStateFromPartBuilt( part_name );

		super.OnPartBuiltServer(player, part_name, action_id );

		UpdateVisuals();
	}

	override void ExpansionUpdateBaseBuildingStateFromPartBuilt( string part_name )
	{
		if ( part_name == m_Expansion_CurrentBuild + "_windowfinished" )
		{
			m_Expansion_HasWindow = true;
		}

		if ( part_name == m_Expansion_CurrentBuild + "_doorfinished" )
		{
			m_Expansion_HasDoor = true;
		}

		if ( part_name == m_Expansion_CurrentBuild + "_gatefinished" )
		{
			m_Expansion_HasGate = true;
		}

		if ( part_name == m_Expansion_CurrentBuild + "_wallfinished" || part_name == m_Expansion_CurrentBuild + "_wallfinished_third" || part_name == m_Expansion_CurrentBuild + "_wallfinished_half" )
		{
			m_Expansion_HasWall = true;
		}
	}

	override void OnPartDismantledServer( notnull Man player, string part_name, int action_id )
	{
		ExpansionCodeLock codelock = ExpansionGetCodeLock();
		if ( codelock )
			codelock.UnlockServer( player, this );

		ExpansionResetBaseBuildingState();

		super.OnPartDismantledServer( player, part_name, action_id );

		UpdateVisuals();
	}

	override void OnPartDestroyedServer( Man player, string part_name, int action_id, bool destroyed_by_connected_part = false )
	{
		ExpansionCodeLock codelock = ExpansionGetCodeLock();
		if ( codelock )
			codelock.UnlockServer( player, this );

		ExpansionResetBaseBuildingState();

		super.OnPartDestroyedServer( player, part_name, action_id, destroyed_by_connected_part );

		UpdateVisuals();
	}

	override void UpdateVisuals()
	{
		if ( m_Expansion_HasWindow )
		{
			ShowSelectionProxy( m_Expansion_CurrentBuild + "_window_ll" );
			ShowSelectionProxy( m_Expansion_CurrentBuild + "_window_lr" );
			ShowSelectionProxy( m_Expansion_CurrentBuild + "_window_rl" );
			ShowSelectionProxy( m_Expansion_CurrentBuild + "_window_rr" );
		} else
		{
			HideSelectionProxy( m_Expansion_CurrentBuild + "_window_ll" );
			HideSelectionProxy( m_Expansion_CurrentBuild + "_window_lr" );
			HideSelectionProxy( m_Expansion_CurrentBuild + "_window_rl" );
			HideSelectionProxy( m_Expansion_CurrentBuild + "_window_rr" );
		}

		if ( m_Expansion_HasDoor )
		{
			ShowSelectionProxy( m_Expansion_CurrentBuild + "_door" );
		} else
		{
			HideSelectionProxy( m_Expansion_CurrentBuild + "_door" );
		}

		if ( m_Expansion_HasGate )
		{
			ShowSelectionProxy( m_Expansion_CurrentBuild + "_gate_l" );
			ShowSelectionProxy( m_Expansion_CurrentBuild + "_gate_r" );
		} else
		{
			HideSelectionProxy( m_Expansion_CurrentBuild + "_gate_l" );
			HideSelectionProxy( m_Expansion_CurrentBuild + "_gate_r" );
		}

		super.UpdateVisuals();
	}

	private bool ExpansionCanAcceptCodeLock( string slot_name )
	{
		if ( slot_name == "Att_ExpansionCodeLock_1" )
		{
			if ( !m_Expansion_HasDoor )
			{
				return false;
			}
		}
		
		if ( slot_name == "Att_ExpansionCodeLock_2" )
		{
			if ( !m_Expansion_HasGate )
			{
				return false;
			}
		}

		return true;
	}
	
	override bool CanDisplayAttachmentCategory( string category_name )
	{
		if ( !super.CanDisplayAttachmentCategory( category_name ) )
		{
			return false;
		}

		if ( category_name == "Material" )
		{
			if ( ExpansionHasCodeLock() && ( m_Expansion_HasGate || m_Expansion_HasDoor ) )
			{
				//! GetHealth cannot be called on client. Using GetHealthLevel instead
				return GetHealthLevel() != GameConstants.STATE_PRISTINE || GetInventory().AttachmentCount() > 1;
			} else {
				//! GetHealth cannot be called on client. Using GetHealthLevel instead
				return GetHealthLevel() != GameConstants.STATE_PRISTINE || !IsLastStage() || GetInventory().AttachmentCount() > 0;
			}
		}

		if ( category_name == "Attachments" )
		{
			if ( !( m_Expansion_HasGate || m_Expansion_HasDoor ) )
			{
				return false;
			}
		}

		return true;
	}

	override bool CanDisplayAttachmentSlot(int slot_id)
	{
		if (!super.CanDisplayAttachmentSlot(slot_id))
		{
			return false;
		}

		string slot_name = InventorySlots.GetSlotName(slot_id);

		if ( !ExpansionCanAcceptCodeLock( slot_name ) )
		{
			return false;
		}
		
		return true;
	}

	override bool CanReceiveAttachment( EntityAI attachment, int slotId )
	{
		if ( !super.CanReceiveAttachment( attachment, slotId ) )
		{
			return false;
		}

		string slot_name = InventorySlots.GetSlotName( slotId );

		if ( !ExpansionCanAcceptCodeLock( slot_name ) )
		{
			return false;
		}
		
		return true;
	}
	
	override bool ExpansionCanOpen( PlayerBase player, string selection )
	{
		if ( !ExpansionIsOpenable() )
			return false;

		if ( m_Expansion_HasWindow && !IsFacingPlayer( player, selection ) && ( !IsMissionClient() || !IsFacingCamera( selection ) ) )
		{
			if ( selection == (m_Expansion_CurrentBuild + "_window_ll") && GetAnimationPhase( m_Expansion_CurrentBuild + "_window_ll_rotate" ) < 0.5 )
				return true;
			if ( selection == (m_Expansion_CurrentBuild + "_window_lr") && GetAnimationPhase( m_Expansion_CurrentBuild + "_window_lr_rotate" ) < 0.5 )
				return true;
			if ( selection == (m_Expansion_CurrentBuild + "_window_rl") && GetAnimationPhase( m_Expansion_CurrentBuild + "_window_rl_rotate" ) < 0.5 )
				return true;
			if ( selection == (m_Expansion_CurrentBuild + "_window_rr") && GetAnimationPhase( m_Expansion_CurrentBuild + "_window_rr_rotate" ) < 0.5 )
				return true;
		}

		if ( m_Expansion_HasDoor && ( !ExpansionIsLocked() || IsKnownUser( player ) ) )
		{
			if ( selection == (m_Expansion_CurrentBuild + "_door") && GetAnimationPhase( m_Expansion_CurrentBuild + "_door_rotate" ) < 0.5 )
				return true;
		}

		if ( m_Expansion_HasGate && ( !ExpansionIsLocked() || IsKnownUser( player ) ) )
		{
			if ( selection == (m_Expansion_CurrentBuild + "_gate_l") && GetAnimationPhase( m_Expansion_CurrentBuild +"_gate_l_rotate" ) < 0.5 )
				return true;
			if ( selection == (m_Expansion_CurrentBuild + "_gate_r") && GetAnimationPhase( m_Expansion_CurrentBuild + "_gate_r_rotate" ) < 0.5 )
				return true;
		}

		return false;
	}
	
	override bool ExpansionCanClose( PlayerBase player, string selection )
	{
		return CanClose( selection );
	}

	override bool CanClose( string selection )
	{
		if ( !ExpansionIsOpenable() )
			return false;

		if ( m_Expansion_HasWindow )
		{
			if ( selection == (m_Expansion_CurrentBuild + "_window_ll") && GetAnimationPhase( m_Expansion_CurrentBuild + "_window_ll_rotate" ) > 0.5 )
				return true;
			if ( selection == (m_Expansion_CurrentBuild + "_window_lr") && GetAnimationPhase( m_Expansion_CurrentBuild + "_window_lr_rotate" ) > 0.5 )
				return true;
			if ( selection == (m_Expansion_CurrentBuild + "_window_rl") && GetAnimationPhase( m_Expansion_CurrentBuild + "_window_rl_rotate" ) > 0.5 )
				return true;
			if ( selection == (m_Expansion_CurrentBuild + "_window_rr") && GetAnimationPhase( m_Expansion_CurrentBuild + "_window_rr_rotate" ) > 0.5 )
				return true;
		}

		if ( m_Expansion_HasDoor )
		{
			if ( selection == (m_Expansion_CurrentBuild + "_door") && GetAnimationPhase( m_Expansion_CurrentBuild + "_door_rotate" ) > 0.5 )
				return true;
		}

		if ( m_Expansion_HasGate )
		{
			if ( selection == (m_Expansion_CurrentBuild + "_gate_l") && GetAnimationPhase( m_Expansion_CurrentBuild +"_gate_l_rotate" ) > 0.5 )
				return true;
			if ( selection == (m_Expansion_CurrentBuild + "_gate_r") && GetAnimationPhase( m_Expansion_CurrentBuild + "_gate_r_rotate" ) > 0.5 )
				return true;
		}

		return false;
	}
	
	override void Expansion_Open( string selection ) 
	{	
		if ( IsMissionHost() && m_Expansion_HasWindow )
		{
			if (selection == (m_Expansion_CurrentBuild + "_window_ll") || m_Expansion_IsOpened1)
			{
				m_Expansion_IsOpened1 = true;
				SetAnimationPhase( m_Expansion_CurrentBuild + "_window_ll_rotate", 1 );
			}
			if (selection == (m_Expansion_CurrentBuild + "_window_lr") || m_Expansion_IsOpened2)
			{
				m_Expansion_IsOpened2 = true;
				SetAnimationPhase( m_Expansion_CurrentBuild + "_window_lr_rotate", 1 );
			}
			if (selection == (m_Expansion_CurrentBuild + "_window_rl") || m_Expansion_IsOpened3)
			{
				m_Expansion_IsOpened3 = true;
				SetAnimationPhase( m_Expansion_CurrentBuild + "_window_rl_rotate", 1 );
			}
			if (selection == (m_Expansion_CurrentBuild + "_window_rr") || m_Expansion_IsOpened4)
			{
				m_Expansion_IsOpened4 = true;
				SetAnimationPhase( m_Expansion_CurrentBuild + "_window_rr_rotate", 1 );
			}

			m_Expansion_IsOpened = true;
		}

		if ( IsMissionHost() && m_Expansion_HasDoor )
		{
			if (selection == (m_Expansion_CurrentBuild + "_door") || m_Expansion_IsOpened)
				SetAnimationPhase( m_Expansion_CurrentBuild + "_door_rotate", 1 );

			m_Expansion_IsOpened = true;
		}

		if ( IsMissionHost() && m_Expansion_HasGate )
		{
			if (selection == (m_Expansion_CurrentBuild + "_gate_l") || m_Expansion_IsOpened1)
			{
				m_Expansion_IsOpened1 = true;
				SetAnimationPhase( m_Expansion_CurrentBuild + "_gate_l_rotate", 1 );
			}
			if (selection == (m_Expansion_CurrentBuild + "_gate_r") || m_Expansion_IsOpened2)
			{
				m_Expansion_IsOpened2 = true;
				SetAnimationPhase( m_Expansion_CurrentBuild + "_gate_r_rotate", 1 );
			}

			m_Expansion_IsOpened = true;
		}

		SetSynchDirty();
	}
	
	override void Expansion_Close( string selection ) 
	{	
		if ( GetGame().IsServer() && m_Expansion_HasWindow )
		{
			if (selection == (m_Expansion_CurrentBuild + "_window_ll"))
			{
				m_Expansion_IsOpened1 = false;
				SetAnimationPhase( m_Expansion_CurrentBuild + "_window_ll_rotate", 0 );
			}
			if (selection == (m_Expansion_CurrentBuild + "_window_lr"))
			{
				m_Expansion_IsOpened2 = false;
				SetAnimationPhase( m_Expansion_CurrentBuild + "_window_lr_rotate", 0 );
			}
			if (selection == (m_Expansion_CurrentBuild + "_window_rl"))
			{
				m_Expansion_IsOpened3 = false;
				SetAnimationPhase( m_Expansion_CurrentBuild+ "_window_rl_rotate", 0 );
			}
			if (selection == (m_Expansion_CurrentBuild + "_window_rr"))
			{
				m_Expansion_IsOpened4 = false;
				SetAnimationPhase( m_Expansion_CurrentBuild + "_window_rr_rotate", 0 );
			}

			if (GetAnimationPhase( m_Expansion_CurrentBuild + "_window_ll_rotate" ) < 0.5)
				m_Expansion_IsOpened = true;
			else if (GetAnimationPhase( m_Expansion_CurrentBuild + "_window_lr_rotate" ) < 0.5)
				m_Expansion_IsOpened = true;
			else if (GetAnimationPhase( m_Expansion_CurrentBuild + "_window_rl_rotate" ) < 0.5)
				m_Expansion_IsOpened = true;
			else if (GetAnimationPhase( m_Expansion_CurrentBuild + "_window_rr_rotate" ) < 0.5)
				m_Expansion_IsOpened = true;
			else
				m_Expansion_IsOpened = false;
		}

		if ( IsMissionHost() && m_Expansion_HasDoor )
		{
			if (selection == (m_Expansion_CurrentBuild + "_door"))
				SetAnimationPhase( m_Expansion_CurrentBuild + "_door_rotate", 0 );

			if (GetAnimationPhase( m_Expansion_CurrentBuild + "_door_rotate" ) < 0.5)
				m_Expansion_IsOpened = true;
			else
				m_Expansion_IsOpened = false;
		}

		if ( IsMissionHost() && m_Expansion_HasGate )
		{
			if (selection == (m_Expansion_CurrentBuild + "_gate_l"))
			{
				m_Expansion_IsOpened1 = false;
				SetAnimationPhase( m_Expansion_CurrentBuild + "_gate_l_rotate", 0 );
			}
			if (selection == (m_Expansion_CurrentBuild + "_gate_r"))
			{
				m_Expansion_IsOpened2 = false;
				SetAnimationPhase( m_Expansion_CurrentBuild + "_gate_r_rotate", 0 );
			}

			if (GetAnimationPhase( m_Expansion_CurrentBuild + "_gate_l_rotate" ) < 0.5 && GetAnimationPhase( m_Expansion_CurrentBuild + "_gate_r_rotate" ) < 0.5)
				m_Expansion_IsOpened = true;
			else
				m_Expansion_IsOpened = false;
		}
		
		SetSynchDirty();
	}

	override void UnlockAndOpen( string selection ) 
	{
		super.ExpansionUnlock();

		if ( m_Expansion_HasDoor )
		{
			Expansion_Open( m_Expansion_CurrentBuild + "_door" );
		} else if ( m_Expansion_HasGate )
		{
			Expansion_Open( m_Expansion_CurrentBuild + "_gate_l" );
			Expansion_Open( m_Expansion_CurrentBuild + "_gate_r" );
		}
	}

	override void CloseAndLock( string selection )
	{
		if ( m_Expansion_HasDoor && ExpansionIsOpened() )
		{
			Expansion_Close( m_Expansion_CurrentBuild + "_door" );
		} else if ( m_Expansion_HasGate )
		{
			if ( GetAnimationPhase( m_Expansion_CurrentBuild + "_gate_l_rotate" ) > 0.5 )
				Expansion_Close( m_Expansion_CurrentBuild + "_gate_l" );
			if ( GetAnimationPhase( m_Expansion_CurrentBuild + "_gate_r_rotate" ) > 0.5 )
				Expansion_Close( m_Expansion_CurrentBuild + "_gate_r" );
		}

		ExpansionLock();
	}
}
